add_array(n, p, q, r) 
[ <#n: { v : int | T }, p: { v : int | (=> (and (>= i 0) (<= i (- n 1))) (> v 0)) } ref (0, n - 1, 0.5), q: { v : int | (=> (and (>= i 0) (<= i (- n 1))) (> v 0)) } ref (0, n - 1, 0.5), r: { v : int | T } ref (0, n - 1, 1.0)> -> 
  <#n: { v : int | T }, p: { v : int | T } ref (0, n - 1, 0.0), q: { v : int | T } ref (0, n - 1, 0.0), r: { v : int | (=> (and (>= i 0) (<= i (- n 1))) (> v 0)) } ref (0, n - 1, 0.5) | { v : int | T }> ]
{
  if n <= 0 then {
    1
  } else {
    let x = *p in
    let y = *q in
    let z = x + y in
    r := z;
    let p1 = p + 1 in
    let q1 = q + 1 in
    let r1 = r + 1 in
    let m = n - 1 in
    let d = add_array(m, p1, q1, r1) in
    alias(r1 = r + 1);
    1
  }
}

init_x(n, x, p) 
[ <#n: { v : int | T }, x: { v : int | (> v 0) }, p: { v : int | T } ref (0, n - 1, 1.0)> -> 
  <#n: { v : int | T }, x: { v : int | (> v 0) }, p: { v : int | (=> (and (>= i 0) (<= i (- n 1))) (> v 0)) } ref (0, n - 1, 1.0) | { v : int | T }> ]
{
  if n <= 0 then {
    1
  } else {
    p := x;
    let q = p + 1 in
    let m = n - 1 in
    let d = init_x(m, x, q) in
    alias(q = p + 1);
    1
  }
}

{
  let p = mkarray 5 in
  let q = mkarray 5 in
  let r = mkarray 5 in
  let m = 5 in
  let x = 1 in
  let y = 2 in
  let d = init_x(m, x, p) in
  let d1 = init_x(m, y, q) in
  let d2 = add_array(m, p, q, r) in
  let rr = *r in
  assert(rr > 0);
  ()
}
